# Display controller for printing text from a UART port.

reset:
    draw clear 0 0 0
    drawflush display1

reset_loop:
    sensor enabled switch1 @enabled
    jump reset_loop equal enabled false

    setrate 5000

    set CONFIG processor1
    set UART bank1

    read UART_FIFO_MODULO CONFIG "UART_FIFO_MODULO"

    read rptr UART 510

    draw reset
    draw color 255 255 255 255
    draw clear 0 0 0
    drawflush display1

    set CHAR_WIDTH 7
    set CHAR_HEIGHT 13

    set BASE_X 8
    set BASE_Y 508

    # 505 = 71 characters
    # 498 = 70 characters
    set WRAP_X 505

    # TODO: test this
    set WRAP_Y 8

    set cur_x BASE_X
    set next_x BASE_X

    set cur_y BASE_Y
    set next_y BASE_Y

main:
    sensor enabled switch1 @enabled
    jump reset equal enabled false
    read wptr UART 511
    jump main equal rptr wptr

loop:
    op add index rptr 256
    read char UART index

    jump loop__no_print equal char 13 # cr

    jump loop__newline equal char 10 # lf

    op add next_x next_x CHAR_WIDTH
    jump loop__print_now equal char 32 # space
    jump loop__same_line lessThanEq next_x WRAP_X

loop__newline:
    set next_x BASE_X
    op sub next_y next_y CHAR_HEIGHT

    jump loop__no_overflow_y greaterThanEq next_y WRAP_Y
    set next_y BASE_Y
loop__no_overflow_y:

loop__print_now:
    draw print cur_x cur_y @topLeft
    set cur_x next_x
    set cur_y next_y

    jump loop__no_print notEqual cur_x BASE_X # space in middle of line

    # if we reach this point, we're at the start of a new line, so clear it
    draw color 0 0 0 255
    op sub y cur_y CHAR_HEIGHT
    op sub y y 3
    draw rect 6 y 500 CHAR_HEIGHT
    draw color 255 255 255 255

    drawflush display1

    jump loop__no_print equal char 32 # space at start of line
    jump loop__no_print equal char 10 # lf

    # if we reach this point, we're printing a character after moving to a new line, so shift next_x again
    op add next_x next_x CHAR_WIDTH

loop__same_line:
    printchar char

loop__no_print:
    op add rptr rptr 1
    op mod rptr rptr UART_FIFO_MODULO
    write rptr UART 510

    read wptr UART 511
    jump loop notEqual rptr wptr

    draw print cur_x cur_y @topLeft
    set cur_x next_x
    set cur_y next_y

    drawflush display1

    jump main always
